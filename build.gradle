import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id("com.starter.config")
    id("com.starter.versioning")
    id("com.gradleup.auto.manifest")
    id("com.osacky.doctor") version("0.7.0")
    id("com.starter.easylauncher") version("4.1.0") apply(false)
}

autoManifest {
    packageName.set("io.github.wykopmobilny")
}

commonConfig {
    javaFilesAllowed false
}

allprojects {
    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            freeCompilerArgs = freeCompilerArgs +
                "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi" +
                "-Xopt-in=kotlin.RequiresOptIn" +
                "-Xopt-in=kotlin.contracts.ExperimentalContracts" +
                "-Xopt-in=kotlin.time.ExperimentalTime" +
                "-Xopt-in=kotlinx.coroutines.FlowPreview"
        }
    }
    ["com.android.library", "com.android.application"].forEach {
        pluginManager.withPlugin(it) {
            android.packagingOptions {
                exclude 'META-INF/*.kotlin_module'
            }
        }
    }
    pluginManager.withPlugin("com.android.library") {
        android.variantFilter {
            ignore = buildType.name != "debug"
        }
    }
    pluginManager.withPlugin("com.android.application") {
        android.buildTypes.configureEach {
            setMatchingFallbacks("debug")
        }
    }
}
